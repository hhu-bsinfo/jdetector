plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.freudasoft.gradle-cmake-plugin' version '0.0.2'
    id 'com.jfrog.bintray' version '1.8.4'
    id "com.jfrog.artifactory" version "4.7.5"
    id 'de.fuerstenau.buildconfig' version '1.1.8'
    id 'com.palantir.git-version' version '0.11.0'
}

group = 'de.hhu.bsinfo'
version version = '0.1.0' + (Boolean.valueOf(System.getProperty('release')) ? '' : '-SNAPSHOT')

repositories {
    mavenCentral()
    jcenter()

    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

compileJava {
    sourceCompatibility = '8'
    targetCompatibility = '8'
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

cmake {
    sourceFolder = file(project.projectDir.toString())
    buildConfig = 'Release'
    buildTarget = 'install'
    buildClean = true
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

wrapper {
    gradleVersion = "5.4.1"
}

buildConfig {
    def gitVersion = versionDetails()

    appName = project.name
    version = project.version

    clsName = 'BuildConfig'
    packageName = 'de.hhu.bsinfo.jdetector.generated'

    charset = 'UTF-8'

    buildConfigField 'String', 'BUILD_DATE', new Date().format('yyyy-MM-dd HH:mm:ss')
    buildConfigField 'String', 'GIT_COMMIT', gitVersion.gitHash
    buildConfigField 'String', 'GIT_BRANCH', gitVersion.branchName
}

apply from: 'publish.gradle'
apply from: 'bintray.gradle'
apply from: 'artifactory.gradle'
